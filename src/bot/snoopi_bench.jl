################################################################
"""
    timesum(snoop)

Calculates and prints the total time measured by a snoop macro.

It is used inside @snoopi_bench. Julia can cache inference results so to measure the effect of adding _precompile_() sentences generated by snoopi to your package, use the [`@snoopi_bench`](@ref). This benchmark measures inference time taken during loading and running of a package.

# Examples
```julia
using SnoopCompile
data = @snoopi begin
    using MatLang
    MatLang_rootpath = dirname(dirname(pathof("MatLang")))

    include("\$MatLang_rootpath/test/runtests.jl")
end
println(timesum(data))
```

## Manual Benchmark (withtout using [`@snoopi_bench`](@ref))
- dev your package

- comment the precompile part of your package (`include()` and `_precompile_()`)
- run the following benchmark
- restart Julia

- uncomment the precompile part of your package (`include()` and `_precompile_()`)
- run the following benchmark
- restart Julia

### Benchmark
```julia
using SnoopCompile

println("Package load time:")
loadSnoop = @snoopi using MatLang

timesum(loadSnoop)

println("Running Examples/Tests:")
runSnoop = @snoopi begin
    using MatLang

    MatLang_rootpath = dirname(dirname(pathof("MatLang")))
    include("\$MatLang_rootpath/test/runtests.jl")
end

timesum(runSnoop)
```
"""
function timesum(snoop::Vector{Tuple{Float64, Core.MethodInstance}})
    if isempty(snoop)
        return 0.0
    else
        return sum(first, snoop)
    end
end
################################################################
function snoopi_bench(modul::Module, config::BotConfig, snoop_script::Expr)

    package_name = config.package_name
    package_path = pathof_noload(package_name)

    ################################################################
    # quote end generates $ which doesn't work in commands
    # TODO no escape is done for snoop_script!!!
    # TODO
    # data = Core.eval(  $modul, snoopi($(esc(snoop_script)))  )
    # TODO use code directly for now
    # no filter in the benchmark
    juliaCode = """
        using SnoopCompile
        empty!(SnoopCompile.__inf_timing__)
        SnoopCompile.start_timing()
        try
            $(string(snoop_script));
        finally
            SnoopCompile.stop_timing()
        end
        data = SnoopCompile.sort_timed_inf(0.0)
        @info(timesum(data))
    """
    julia_cmd = `julia --project=@. -e $juliaCode`
    out = quote
        package_sym = Symbol($package_name)
        ################################################################
        using SnoopCompile
        @info("""*******************
        Benchmark Started
        *******************
        """)
        ################################################################
        @info("""Precompile Deactivated Inference Benchmark
        ------------------------
        """)
        precompile_deactivator($package_path);
        ### Log the compiles
        run($julia_cmd)
        ################################################################
        @info("""Precompile Activated Inference Benchmark
        ------------------------
        """)
        precompile_activator($package_path);
        ### Log the compiles
        run($julia_cmd)
        @info("""*******************
        Benchmark Finished
        *******************
        """)
    end
    return out
end

################################################################
"""
    @snoopi_bench botconfig::BotConfig, snoop_script::Expr

Performs an infertime benchmark by activating and deactivating the _precompile_()
# Examples
Benchmarking the load infer time
```julia
using SnoopCompile

println("loading infer benchmark")
@snoopi_bench BotConfig("MatLang") begin
 using MatLang
end
```

Benchmarking the example infer time
```julia
SnoopCompile

println("examples infer benchmark")
@snoopi_bench BotConfig("MatLang") begin
    using MatLang
    MatLang_rootpath = dirname(dirname(pathof("MatLang")))

    include("\$MatLang_rootpath/examples/Language_Fundamentals/usage_Matrices_and_Arrays.jl")
    include("\$MatLang_rootpath/examples/Language_Fundamentals/Data_Types/usage_Numeric_Types.jl")
end
```
"""
macro snoopi_bench(configExpr, snoop_script)
    config = eval(configExpr)
    out = snoopi_bench(__module__, config, snoop_script)
    return out
end

macro snoopi_bench(package_name::AbstractString, snoop_script)
    f, l = __source__.file, __source__.line
    @warn "Replace `\"$package_name\"` with `BotConfig(\"$package_name\")`. That syntax will be deprecated in future versions. \n Happens at $f:$l"

    config = BotConfig(package_name)

    out = snoopi_bench(__module__, config, snoop_script)
    return out
end

################################################################
function snoopi_bench(modul::Module, config::BotConfig)

    package_name = config.package_name

    package = Symbol(package_name)
    package_rootpath = dirname(dirname(pathof_noload(package_name)))
    runtestpath = "$package_rootpath/test/runtests.jl"

    snoop_script = quote
        using $(package);
        include($runtestpath);
    end
    out = snoopi_bench(modul, config, snoop_script)
    return out
end

"""
    @snoopi_bench config::BotConfig

Benchmarking the infer time of the tests:
```julia
@snoopi_bench BotConfig("MatLang")
```
"""
macro snoopi_bench(configExpr)
    config = eval(configExpr)
    out = snoopi_bench(__module__, config)
    return out
end

macro snoopi_bench(package_name::AbstractString)
    f, l = __source__.file, __source__.line
    @warn "Replace `\"$package_name\"` with `BotConfig(\"$package_name\")`. That syntax will be deprecated in future versions. \n Happens at $f:$l"

    config = BotConfig(package_name)

    out = snoopi_bench(__module__, config)
    return out
end
